@Metadata.layer: #CORE
@UI.headerInfo : { 
        typeName: 'Travel',
        typeNamePlural: 'Travels',
        title: {value: 'TravelId'},
        description: { value : 'CustomerId' }
        }

@UI.presentationVariant: [{
                sortOrder: [{by: 'Lastchangedat',direction: #DESC }],
                visualizations: [{type: #AS_LINEITEM}]
}]
annotate entity ZKE_UM_TRAVEL
    with 
{
    @UI.facet: [{
                 id: 'TravelHeaderPrice',
                 purpose: #HEADER,
                 type: #DATAPOINT_REFERENCE,
                 position: 10,
                 targetQualifier: 'PriceData' 
            },{ 
                id: 'TravelHeaderStatus',
                purpose: #HEADER,
                type: #DATAPOINT_REFERENCE,
                position: 20,
                targetQualifier: 'StatusData' 
             },{ 
                id: 'GeneralInfo',
                type: #COLLECTION,
                label: 'General Info',
                position: 10
             },{ 
                id: 'Travel',
                type: #IDENTIFICATION_REFERENCE,
                purpose: #STANDARD,
                label:'General Data',
                parentId: 'GeneralInfo',
                position: 10
             },{ 
                id: 'Dates',
                type: #FIELDGROUP_REFERENCE,
                label:'TravelDate',
                parentId: 'GeneralInfo',
                position: 20,
                targetQualifier: 'DateGroup'
             },{ 
                id: 'Prices',
                type: #FIELDGROUP_REFERENCE,
                label:'Price data',
                parentId: 'GeneralInfo',
                position: 30,
                targetQualifier: 'PriceGroup'
             }]
    @UI: {selectionField: [{position : 10 }],
            lineItem: [{position : 10 }],
            identification: [{position: 10 }] }
    TravelId;
    @UI: {selectionField: [{position : 20 }],
            lineItem: [{position : 20 }],
             identification: [{position: 20 }] }
    AgencyId;
    @UI: {selectionField: [{position : 30 }],
            lineItem: [{position : 30 }],
             identification: [{position: 30 }]}
    CustomerId;
    @UI: {selectionField: [{position : 40 }],
            lineItem: [{position : 40 }]}
    @UI.fieldGroup: [{qualifier: 'DateGroup',position: 10 }]
    BeginDate;
    @UI. lineItem: [{position : 50 }]
    @UI.dataPoint: { qualifier: 'PriceData', title: 'Price Data' }
    @UI.fieldGroup: [{qualifier: 'PriceGroup',position: 10 }]
    TotalPrice;
     @UI.lineItem: [{position : 51 }]
    CurrencyCode;
//    @UI.lineItem:  [{position : 11 }]
//    Description;
     @UI: {selectionField: [{position : 50 }],
            lineItem: [{position : 60,importance: #HIGH, type: #FOR_ACTION, label: 'Set Status Booked', dataAction: 'set_booked_status' }],
            identification: [{position: 40 }]}
     @UI.dataPoint: { qualifier: 'StatusData', title: 'Travel Status' }
    Status;
   @UI.lineItem:  [{position : 70 }]
    Lastchangedat;
    /* Associations */
   @UI.fieldGroup: [{qualifier: 'DateGroup',position: 20 }]   
    EndDate;
      @UI.fieldGroup: [{qualifier: 'PriceGroup',position: 20 }]  
    BookingFee;
 
}